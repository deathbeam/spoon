-- vim: set ft=rb:

-- Load OpenFL libraries
import openfl.display.Bitmap
import openfl.display.Sprite
import openfl.events.Event
import openfl.events.KeyboardEvent
import openfl.ui.Keyboard
import openfl.Assets

-- Extend self by OpenFL Sprite
class self < Sprite
  def logo : Sprite
  def movingDown : Bool
  def movingLeft : Bool
  def movingRight : Bool
  def movingUp : Bool

  def new()
    super()

    logo = Sprite.new()
    logo.addChild(Bitmap.new(Assets.getBitmapData("assets/openfl.png")))
    logo.x = 100
    logo.y = 100
    logo.buttonMode = true
    addChild(logo)

    stage.addEventListener(KeyboardEvent.KEY_DOWN, onKeyDown)
    stage.addEventListener(KeyboardEvent.KEY_UP, onKeyUp)
    stage.addEventListener(Event.ENTER_FRAME, onEnterFrame)
  end

  ---
  Event handlers
  ---

  def onKeyDown(event)
    if(event.keyCode == Keyboard.DOWN)
      movingDown = true
    elsif(event.keyCode == Keyboard.LEFT)
      movingLeft = true
    elsif(event.keyCode == Keyboard.RIGHT)
      movingRight = true
    elsif(event.keyCode == Keyboard.UP)
      movingUp = true
    end
  end

  def onKeyUp(event)
    if(event.keyCode == Keyboard.DOWN)
      movingDown = false
    elsif(event.keyCode == Keyboard.LEFT)
      movingLeft = false
    elsif(event.keyCode == Keyboard.RIGHT)
      movingRight = false
    elsif(event.keyCode == Keyboard.UP)
      movingUp = false
    end
  end

  def onEnterFrame(event)
    if(movingDown)
      logo.y += 5
    end

    if(movingLeft)
      logo.x -= 5
    end

    if(movingRight)
      logo.x += 5
    end

    if(movingUp)
      logo.y -= 5
    end
  end
end
