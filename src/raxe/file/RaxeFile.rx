import raxe.script.RaxeScript
import sys.io.File
import sys.FileSystem

class self
  def script : RaxeScript

  def new(path : String)
    script = createScript()
    script.execute(script.parse(File.getContent(path)))
  end

  def run(task : String)
    def fn = script.variables.get(task)
    fn()
  end

  def createScript() : RaxeScript
    def script = RaxeScript.new()

    script.variables.set("echo", do(msg : String)
      Sys.println(msg)
    end)

    script.variables.set("sh", do(cmd : String, ?args : Array<String>)
      Sys.command(cmd, args)
    end)

    script.variables.set("cp", do(from : String, to : String)
      File.copy(from, to)
    end)

    script.variables.set("mv", do(from : String, to : String)
      FileSystem.rename(from, to)
    end)

    script.variables.set("rm", do(path : String)
      if(FileSystem.isDirectory(path))
        FileSystem.deleteDirectory(path)
      else
        FileSystem.deleteFile(path)
      end
    end)

    script.variables.set("env", {
      get: do(key : String)
        return Sys.getEnv(key)
      end,
      set: do(key : String, value : String)
        Sys.putEnv(key, value)
      end,
    })

    return script
  end
end
