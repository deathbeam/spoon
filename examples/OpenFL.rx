# vim: set ft=rb:

# Load OpenFL libraries
import openfl.display.Bitmap
import openfl.display.Sprite
import openfl.events.Event
import openfl.events.KeyboardEvent
import openfl.ui.Keyboard
import openfl.Assets

# Extend @ by OpenFL Sprite
class @ < Sprite
  def logo : Sprite
  def movingDown : Bool
  def movingLeft : Bool
  def movingRight : Bool
  def movingUp : Bool

  def new()
    super()

    logo = new Sprite()
    logo.addChild(new Bitmap(Assets.getBitmapData("assets/openfl.png")))
    logo.x = 100
    logo.y = 100
    logo.buttonMode = true
    addChild(logo)

    stage.addEventListener(KeyboardEvent.KEY_DOWN, onKeyDown)
    stage.addEventListener(KeyboardEvent.KEY_UP, onKeyUp)

    stage.addEventListener(Event.ENTER_FRAME, def(event)
      if(movingDown)
        logo.y += 5
      end

      if(movingLeft)
        logo.x -= 5
      end

      if(movingRight)
        logo.x += 5
      end

      if(movingUp)
        logo.y -= 5
      end
    end)
  end

  ###
  Event handlers
  ###

  def onKeyDown(event)
    switch(event.keyCode)
      when(Keyboard.DOWN)
        movingDown = true
      when(Keyboard.LEFT)
        movingLeft = true
      when(Keyboard.RIGHT)
        movingRight = true
      when(Keyboard.UP)
        movingUp = true
    end
  end

  def onKeyUp(event)
    switch(event.keyCode)
      when(Keyboard.DOWN)
        movingDown = false
      when(Keyboard.LEFT)
        movingLeft = false
      when(Keyboard.RIGHT)
        movingRight = false
      when(Keyboard.UP)
        movingUp = false
    end
  end
end
